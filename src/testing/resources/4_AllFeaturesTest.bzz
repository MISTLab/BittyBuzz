# Test global variables
x = nil  # Test nil
x = 1    # Test int
x = 3.14 # Test float

# Test native closure
function add(a, b) {
    var c = a + b # Test local variables
    return c
}
# Test native closure call
y = add(42, 21)
print("y = ", y)

# Test lambda closure
lambda = function(a, b, c) {
    print(a, "; ", b, "; ", c)
}
# Test lambda closure call
lambda(-12, 2.91, nil)
lambda = nil

lambdaGen = function(c) {
    return function(a, b) {
        print(a, "; ", b, "; ", c)
    }
}
lambdaGen("c")("a", "b")

# Test string
textToLog = "Hello, world!"
# Test C closure call
print('Logging "', textToLog, '"')
textToLog = nil

# Test Comparisons
if (x > 3)
    print("x is too big")

if (x > 3)
    print("x is too big")
else if (x < 3)
    print("x is too small")
else
    print("maybe I just don't like x")

if ((x < 3) or (x > 3))
    print("x is not 3")
x = nil

# Test loop
z = 0
loc = function() {
  z = z + 1
  print(z)
}
while (z < 5)
   loc()

loc = nil
z = nil

# Test table
t = { .x=3 }
print("t = ", t)
print("t.x = ", t.x)       # dot syntax
print("t['x'] = ", t["x"]) # string syntax
t = nil

# Test : tables are always passed by reference
t1 = { .x=3 }
t2 = t1      # now t2 points to the contents of t1 -> no deep copy
t2.x = 5
print(t1.x)    # prints 5, not 3!
t1 = nil
t2 = nil

function foo() {
  print("foo(); ", self)
  return { # Table with 2 elements
    .x = { # Table with 3 elements
      .y = { # Table with 4 elements
        .c = function(s) {
          print(".x.y['c'](); ", self)
          return function(s) {
            print("<anonymous>(); ", self)
          }
        },
        .d = 1,
        .e = 2,
        .f = 3
      },
      .a = 1,
      .b = 2
    },
    .z = 0
  }
}

function getKey() {
  return "c"
}

t = { .bar = foo } # Table with 1 element

foo() # Prints "foo(); [nil]"

# Case with no nested table
t2 = t.bar() # Prints "foo(); [table with 1 elems]"

# Case with 3 nested tables and using 2 ways of accessing fields
t3 = t2.x.y["c"]() # Prints ".x.y['c'](); [table with 4 elems]"

# Case with no nested table and called from global scope
t3() # Prints "<anonymous>(); [table with 4 elems]" <-- Should be "[nil]", this is a bug of Buzz

# Case with all in 1 line
t.bar().x.y["c"]()()

# Case with a closure call to get the table's key
t2["x"].y[getKey()]() # Prints ".x.y['c'](); [table with 4 elems]"

# --- Output:
#   foo(); [nil]
#   foo(); [table with 1 elems]
#   .x.y['c'](); [table with 4 elems]
#   <anonymous>(); [table with 4 elems]
#   foo(); [table with 1 elems]
#   .x.y['c'](); [table with 4 elems]
#   <anonymous>(); [table with 4 elems]
#   .x.y['c'](); [table with 4 elems]
